#!/usr/bin/env zsh

emulate -L zsh
setopt extended_glob no_octal_zeroes no_short_loops null_glob typeset_silent warn_create_global

integer MBEGIN MEND
integer ZSD_PB_SELECTED_WORD
typeset -a ZSD_PB_WORDS ZSD_PB_SPACES ZSD_PB_WORDS_BEGINNINGS
typeset -a match mbegin mend reply
MATCH= REPLY=
ZSD_PB_LEFT= ZSD_PB_RIGHT=

zsd-detect-usage () {
    local -A colors
    autoload -Uz colors && colors 2> /dev/null
    print "$fg[green]Usage:$reset_color zsd-detect [-h/--help] [-v/--verbose] [-q/--quiet] [-n/--noansi] [--cignore <pattern>] $fg_bold[magenta]{file}$reset_color"
    print "The $fg_bold[magenta]file$reset_color will be processed to gather function comments and call trees."
    print "Data extracted by \`zsd-transform' is needed. Supported are Bash and Zsh scripts."
    print
    print "$fg[green]Options:$reset_color"
    print -- "$fg[magenta]-h/--help$reset_color      Usage information"
    print -- "$fg[magenta]-v/--verbose$reset_color   More verbose operation-status output"
    print -- "$fg[magenta]-q/--quiet$reset_color     No status messages"
    print -- "$fg[magenta]-n/--noansi$reset_color    No colors in terminal output"
    print -- "$fg[magenta]--cignore$reset_color      Specify which comment lines should be ignored"
    print
    print -- "Example --cignore options:"
    print -- "--cignore '\\#*FUNCTION:*{{{*'                 - ignore comments like: $fg[green]# FUNCTION: usage {{{$reset_color"
    print -- "--cignore '(\\#*FUNCTION:*{{{*|\\#*FUN:*{{{*)'   - also ignore comments like: $fg[green]# FUN: usage {{{$reset_color"
}

zsd-detect-process-node () {
    local node=$1
    if [[ $node = ${(q)name}/zsd_script_body ]]; then
        command mkdir -p -- Script_Body_
        cd Script_Body_
    else
        if [[ ${node#${(q)name}} = ${node} ]]; then
            local dir=${node/\//_-_}
        else
            local dir=${node##*/}
        fi
        command mkdir -p -- $dir
        cd -- $dir
        PROCESSED+=($node)
    fi
    local subfuns=${call_tree[$node]}
    local -a asubfuns
    asubfuns=(${(z@)subfuns})
    local f
    for f in ${asubfuns[@]}; do
        [[ -n ${PROCESSED[(r)$f]} ]] && continue
        zsd-detect-process-node $f
    done
    cd ..
    PROCESSED[-1]=()
}

# vim: set expandtab filetype=zsh shiftwidth=4 softtabstop=4 tabstop=4:
